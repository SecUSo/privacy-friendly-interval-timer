apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdk 34

    namespace "org.secuso.privacyfriendlyintervaltimer"
    defaultConfig {
        applicationId "org.secuso.privacyfriendlyintervaltimer"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 7
        versionName "1.3.2"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    kotlin {
        jvmToolchain(17)
    }

    lint {
        lintConfig = file("lint.xml")
    }
}

android.applicationVariants.configureEach { variant ->
    variant.outputs.configureEach {
        def appName = "pfa-interval-timer"
        outputFileName = appName + "-${variant.name}-v${variant.versionName}.apk"
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.google.com'}
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.activity:activity-ktx:1.2.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.0-beta1'
    testImplementation 'junit:junit:4.12'

    // Backup
    implementation project(path: ':backup-api')
    def work_version = "2.4.0"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"
    implementation 'androidx.sqlite:sqlite-ktx:2.3.1'

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}
